// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   
   Mux16(a=x, b=false, sel=zx, out=tempoutx);
   Not16(in=tempoutx, out=negoutx);
   Mux16(a=tempoutx, b=negoutx, sel=nx, out=outx);
   Mux16(a=y, b=false, sel=zy, out=tempouty);
   Not16(in=tempouty, out=negouty);
   Mux16(a=tempouty, b=negouty, sel=ny, out=outy);
   And16(a=outx, b=outy, out=temp0);
   Add16(a=outx, b=outy, out=temp1);
   Mux16(a=temp0, b=temp1, sel=f, out=tempout);
   Not16(in=tempout, out=negtempout);
   Mux16(a=tempout, b=negtempout, sel=no, out=out, out[0..7]=final0, out[8..15]=final1, out[15]=ng);
   Or8Way(in=final0, out=or0);
   Or8Way(in=final1, out=or1);
   Or(a=or0, b=or1, out=tempzr);
   Not(in=tempzr, out=zr);
}